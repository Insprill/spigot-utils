plugins {
    id 'java'
    id 'signing'
    id 'maven-publish'
    id 'io.freefair.lombok' version '6.4.3.1'
    id 'io.github.gradle-nexus.publish-plugin' version '1.1.0'
}

group 'net.insprill'
version '0.1.0'

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8
compileJava.options.encoding = 'UTF-8'

repositories {
    mavenCentral()
    maven { url = 'https://repo.papermc.io/repository/maven-public/' }
}

dependencies {
    compileOnly 'org.jetbrains:annotations:23.0.0'
    compileOnly 'org.spigotmc:spigot-api:1.19.2-R0.1-SNAPSHOT'

    testImplementation 'com.github.seeseemelk:MockBukkit-v1.18:2.22.2'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.2'
}

java {
    withSourcesJar()
    withJavadocJar()
}

test {
    useJUnitPlatform()
}

compileTestJava {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
            pom {
                name = project.name
                description = 'A small library of commonly used functions in Spigot plugins'
                url = 'https://github.com/Insprill/spigot-utils'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'https://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'insprill'
                        name = 'Pierce Thompson'
                        url = 'https://github.com/Insprill'
                    }
                }
                scm {
                    url = 'https://github.com/Insprill/spigot-utils'
                    connection = 'scm:git:git://github.com/Insprill/spigot-utils.git'
                    developerConnection = 'scm:git:git@github.com:Insprill/spigot-utils.git'
                }
                issueManagement {
                    system = 'GitHub Issues'
                    url = 'https://github.com/Insprill/spigot-utils/issues'
                }
            }
        }
    }
}
